subprojects {
	apply plugin: 'java'
	apply plugin: 'eclipse'
	
	sourceCompatibility = '1.7'
	targetCompatibility = '1.7'
	version = '1.2'
	
	compileJava.options.encoding = 'UTF-8'
	javadoc.options.encoding = 'UTF-8'

	repositories {
		mavenCentral()
	}
	configurations {
		provided
	}
	sourceSets {
		main {
			compileClasspath = compileClasspath + configurations.provided
		}
	}
	eclipse {
		classpath {
			plusConfigurations += configurations.provided
		}
	}

	dependencies {
		provided "org.vert-x:vertx-core:$vertxVersion"
		provided "org.vert-x:vertx-platform:$vertxVersion"
	}
}

project(':sodabox-common'){
	jar.enabled = true

	dependencies {
		compile fileTree(dir: 'ext-libs', include: '*.*')
		compile 'redis.clients:jedis:2.1.0'
		compile 'com.google.guava:guava:13.0.1'
		compile 'commons-lang:commons-lang:2.6'
		compile 'org.slf4j:slf4j-log4j12:1.6.6'
	}

	task dist( type:Copy, dependsOn: 'jar' ) {
		group = 'sodabox'
		description = "... ^^; "

		into "../../bin/vertx/lib"
		from configurations.compile
		from 'build/libs'
	}
}

project(':sodabox-starter'){
	jar.enabled = true
	
	dependencies {
	}

	task dist( type:Copy, dependsOn: 'jar' ) {
		group = 'sodabox'
		description = "... ^^; "

		into "../../lib"
		from 'build/libs'
	}
}

configure(subprojects.findAll {it.name.startsWith('mod-')}){
	jar.enabled = false
	
	dependencies {
		provided project(':sodabox-common')
	}
	
	task dist( type:Copy, dependsOn: 'classes' ) {
		group = 'sodabox'
		description = "copy vertx module to /mods "

		delete "../../mods/$modulename-v$version"

		into "../../mods/$modulename-v$version"
		from 'build/classes/main'
		from 'src/main/conf'
		into( 'lib' ) {
			from configurations.compile
		}
	}
}

